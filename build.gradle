buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'

group= "com.herobone.heroutils"
archivesBaseName = "heroutils"

sourceCompatibility = targetCompatibility = "1.8"

compileJava {
    sourceCompatibility = targetCompatibility = "1.8"
}

// Define properties file
ext.configFile = file "build.properties"

configFile.withReader {
	// Load config.  It shall from now be referenced as simply config or project.config
	def prop = new Properties()
	prop.load(it)
	project.ext.config = new ConfigSlurper().parse prop
}

version = config.mod_version

minecraft {
    version = config.mc_version + "-" + config.forge_version
	mappings = config.mappings
    runDir = "run"
    makeObfSourceJar = false
}

repositories {
	maven {
		name = "ChickenBones Maven"
		url = "http://chickenbones.net/maven/"
	}
	maven {
		name = "Covers Maven"
		url = "http://maven.covers1624.net"
	}
	maven {
		name = "JEI Maven"
		url = "http://dvs1.progwml6.com/files/maven"
	}
    maven {
        name = "Multipart Maven"
        url = "https://maven.amadornes.com/"
    }
}

dependencies {
	compile "codechicken:CodeChickenLib:${config.mc_version}-${config.ccl_version}:deobf"
	compile "cofh:CoFHCore:${config.mc_version}-${config.cofhcore_version}:deobf"
    compile "MCMultiPart:MCMultiPart:${config.mcmultipart}:deobf"
	compile files("lib/Baubles-1.10.2-1.3.11.jar")
    compile files("lib/Mekanism-1.10.2-9.2.2.301.jar")
}

processResources
{
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

jar {
	classifier = 'universal'
}

task deobfJar(type: Jar) {
	from sourceSets.main.output
	classifier = 'deobf'
}

task srcJar(type: Jar) {
	from sourceSets.main.allSource
	classifier = 'sources'
}

// Tell the artifact system about our extra jars
artifacts {
	archives deobfJar
	archives srcJar
}
